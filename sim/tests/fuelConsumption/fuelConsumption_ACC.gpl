##set encoding iso_8859_1  

set datafile separator ","

# parameters

if (!exists("project")) project= "fuelConsumption_ACC"
if (!exists("label")) label= "carConsumption"

modelName = project.".".label
print "modelName = ",modelName

path="./figs/"

# set style line: linetype lt, point type pt

# if independently color, dash-style, point-style gnuplot 4.2 and later
set style line 99 lt 1 lw 3 pt 4 ps 1.5 linecolor rgb "#1100EE" #blau, solid, open box

set style line 1 lt 1 lw 2 pt 7 ps 1.5  lc rgb "#000000" #schwarz,solid,bullet
set style line 2 lt 1 lw 2 pt 2 ps 1.5  lc rgb "#CC0022" #rot, dash, Kreuz
set style line 3 lt 8 lw 2 pt 4 ps 1.5  #blassrot, offenes Quadrat
set style line 4 lt 6 lw 2 pt 4 ps 1.5  lc rgb "#FFAA00"  #gelb, offenes Quadrat
set style line 5 lt 1 lw 2 pt 5 ps 1.5  lc rgb "#00DD22"  #gruen,solid,ClosedBox
set style line 6 lt 5 lw 2 pt 4 ps 1.5  lc rgb "#00AAAA" #offenes Quadrat
set style line 7 lt 1 lw 2 pt 4 ps 1.5  lc rgb "#1100AA"  #blau,offenes Quadrat
set style line 8 lt 4 lw 2 pt 8 ps 1.5  #lila, aufrechtes geschloss. Dreieck
set style line 9 lt 7 lw 2 pt 9 ps 1.5  lc rgb "#999999"  #grau, aufr. gschl. Dreieck

set style line 11 lt 3 lw 10 pt 7 ps 1.9  lc rgb "#000000" #schwarz,solid,bullet
set style line 21 lt 2 lw 4 pt 7 ps 1.9  lc rgb "#000000" #schwarz,solid,bullet
set style line 12 lt 1 lw 6 pt 2 ps 1.5  lc rgb "#CC0022" #rot, dash, Kreuz
set style line 13 lt 8 lw 6 pt 3 ps 1.2 #blassrot, offener Stern
set style line 14 lt 6 lw 6 pt 4 ps 1.5  lc rgb "#FFAA00"  #gelb, offenes Quadrat
set style line 15 lt 1 lw 6 pt 5 ps 1.5  lc rgb "#00DD22"  #gruen,solid,ClosedBox
set style line 16 lt 5 lw 6 pt 7 ps 1.5  lc rgb "#00CCCC" #offener Kreis
set style line 17 lt 1 lw 6 pt 7 ps 1.5  lc rgb "#1100AA"  #blau,solid,Bullet
set style line 18 lt 4 lw 6 pt 8 ps 1.5 #lila, aufrechtes geschloss. Dreieck
set style line 19 lt 7 lw 6 pt 9 ps 1.5  lc rgb "#999999"  #grau, aufr. gschl. Dreieck


set style line 99  lt 6 lw 3        # Farbe+Dicke fuer optionales Gitternetz
set style line 101 lt 7 lw 1.2  
set style line 102 lt 2 lw 1.2
set style line 103 lt 3 lw 1.2  
set style line 104 lt 9 lw 1.2  
set style line 105 lt 8 lw 1.2  
set style line 106 lt 5 lw 1.2
set style line 107 lt 4 lw 1.2  

max(x,y)    = (x>y) ? x : y
min(x,y)    = (x>y) ? y : x

set parametric

select(x,xmin,xmax)=((x>=xmin)&&(x<=xmax)) ? 0 : 1/0 #-1000
consmax2d=20
#consmax3d=110.
#consmaxColor=33.
dotCmax=40 # max consumption rate in liter/h

unset key

set size 1,1

########################################################
set term post eps color enhanced solid "Helvetica" 22

set yrange [0:2*consmax2d]
set xlabel "Speed (km/h)"
set ylabel "Consumption C_x (l/100km)" 
set key ## at screen 0.6,0.8

set title noenhanced "Zero acceleration, ".modelName
filename= path.modelName.".consumption_const_acc.eps"
set out filename
print "plot ",filename
plot\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,0.5,1.5)) t "Gear 1" w p ls 1,\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,1.5,2.5)) t "Gear 2" w p ls 2,\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,2.5,3.5)) t "Gear 3" w p ls 3,\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,3.5,4.5)) t "Gear 4" w p ls 4,\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,4.5,5.5)) t "Gear 5" w p ls 5,\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,5.5,6.5)) t "Gear 6" w p ls 6,\
  modelName.".JanteOpt" u 1:($6+select($2,-0.02,0.02)+select($7,6.5,7.5)) t "Gear 7" w p ls 7

###############################################


set pm3d 
set pm3d  map                       # color coded xy surface


####### (2) Contourlinien (Linienstil immer bei  splot definieren!) ######

#unset contour              # no contour lines
set contour surface      # Aktiviert Kontourlinien auf 3D-Flaeche (egal bei pm3d map)
#set contour base          # Aktiviert Kontourlinien auf xy-Ebene (egal bei pm3d map)
#set contour both           # Aktiviert Kontourlinien auf beidem
#set cntrparam levels incr 0,2,30
set cntrparam levels discrete 0.01,2,4,6,8,10,12,14,16,18,20,\
      22,24,26,28,30
#      22,24,26,28,30,consmax3d-10

unset clabel  


set title modelName

#####################
unset surface
set nolabel
set palette defined ( 0 "#dd00ff", 20 "blue", 40 "green", 60 "yellow", 80 "orange", 100 "red", 110 "#ffdddd")
#####################

set ylabel "Acceleration (m/s^2)" 
set auto y
#set yrange [0:4]
#set ztics 10

set zrange [0:]
set cbrange[0:dotCmax]
set zrange [0:dotCmax]

set label 1 "C_x (l/100km)" at screen 0.8,0.85

filename=path.modelName.".cons_opt_gear.eps"
print "plot ",filename
set out filename
unset key
splot\
 modelName.".JanteOpt" u 1:2:(select($7,0.5,7.5)+$6) t "" w l ls 99,\
 modelName.".JanteOpt" u 1:2:(select($7,0.5,1.5)+$6) t "Gear 1" w l ls 101,\
 modelName.".JanteOpt" u 1:2:(select($7,1.5,2.5)+$6) t "Gear 2" w l ls 102,\
 modelName.".JanteOpt" u 1:2:(select($7,2.5,3.5)+$6) t "Gear 3" w l ls 103,\
 modelName.".JanteOpt" u 1:2:(select($7,3.5,4.5)+$6) t "Gear 4" w l ls 104,\
 modelName.".JanteOpt" u 1:2:(select($7,4.5,5.5)+$6) t "Gear 5" w l ls 105,\
 modelName.".JanteOpt" u 1:2:(select($7,5.5,6.5)+$6) t "Gear 6" w l ls 106,\
 modelName.".JanteOpt" u 1:2:(select($7,6.5,7.5)+$6) t "Gear 7" w l ls 107


################################################################################

set contour base
set cntrparam bspline

##############################################
set label 1 "c_{spec} (g/kWh)" at screen 0.8,0.85

set cntrparam levels discrete 235,240,249.5,250,250.5,260,270,280,299,300,301,320,350,400,500
set cbrange [250:600]
set zrange [250:600]

set xlabel "Rotation rate (min^{-1})
set ylabel "Torque at crank shaft(Nm)"
set yrange [0:250]

filename=path.modelName.".moment.eps"
print "plot ",filename
set out filename
splot modelName.".specCons" u 1:4:5 t "" w l ls 1  


##############################################
set cntrparam levels inc 0,2,50
unset clabel
set view map
unset surface

set isosamples 21,15

set palette defined ( 0 "#dd00ff", 20 "blue", \
      40 "green", 60 "yellow", 80 "orange", 100 "red",\
      110 "#ffffff")


set ylabel  "mech. engine power (kW)" 
set yrange [0:100]

set cbrange[0:dotCmax]
set zrange [0:dotCmax]
set label 1 "Cons. rate (l/h)" at screen 0.75,0.85
set xtics 1000

filename=path.modelName.".dotC.eps"
print "plot ",filename
set out filename
splot modelName.".specCons" u 1:2:3 t "" w l ls 1

###################


