# gnuplot command file

# parameterised test_trafficlights command file
# invoke using -e option to set parameters, eg:
# gnuplot -e "lang='de'" test_trafficlights_IDM.gpl

# set the output type and size
set terminal postscript eps enhanced color solid font "Helvetica" 26
set size 1,1

# parameters
#if (!exists("inputPath")) inputPath = "csv/"
#if (!exists("outputPath")) outputPath = "figs/"
if (!exists("inputPath")) inputPath = "./"
if (!exists("outputPath")) outputPath = "./"
if (!exists("lang")) lang = "en"
if (!exists("ldm")) ldm = "IDM"
origin=1
#print "lang=".lang
#print "LDM=".ldm

# variables
baseFile = "test_trafficlights_".ldm

# input files
trafficlightFile = inputPath.baseFile.".trafficlights.road_1.csv"
carFile(n) = sprintf(inputPath.baseFile.".car.origin_%d.%06d.csv",origin,n)

# output files
trafficlightsOutput = outputPath.baseFile.".trajectories.eps"


########  Languages  #####################################

# English
timeLabel = "Time (s)"
positionLabel = "Position (km)"
car(n) = sprintf("Car %d",n)

# Deutsch
if (lang eq "de")\
  timeLabel = "Zeit (s)";\
  positionLabel = "Ort (km)";\
  car(n) = sprintf("Kfz %d",n)


########  Line Styles  ###################################

set style line 1 linetype 7 linewidth 3 pointtype 7 pointsize 1.5
set style line 2 linetype 2 linewidth 4 pointtype 5 pointsize 1.5
set style line 3 linetype 3 linewidth 4 pointtype 9 pointsize 1.5
set style line 4 linetype 4 linewidth 4 pointtype 11 pointsize 1.5
set style line 5 linetype 7 linewidth 4 pointtype 13 pointsize 1.5

# color status
#set palette defined ( 0 "gray", 1 "#98fb98", 2 "blue", 3 "cyan", 4 "#ff4500", 5 "black")
set style line 10 pointtype 5 pointsize 1 linecolor rgb "gray"
set style line 11 pointtype 5 pointsize 1 linecolor rgb "#98fb98"
set style line 12 pointtype 5 pointsize 1 linecolor rgb "blue"
set style line 13 pointtype 5 pointsize 1 linecolor rgb "cyan"
set style line 14 pointtype 5 pointsize 1 linecolor rgb "#ff4500"
set style line 15 pointtype 5 pointsize 1 linecolor rgb "black"

# traffic light phases
#  0=green, 1=green_red (amber1), 2=red, 3=red_green (amber2)
set style line 16 pointtype 5 pointsize 1 linecolor rgb "green"
set style line 17 pointtype 5 pointsize 1 linecolor rgb "#ffa500"
set style line 18 pointtype 5 pointsize 1 linecolor rgb "red"
set style line 19 pointtype 5 pointsize 1 linecolor rgb "yellow"


########  Plot  ##########################################

deltaTL(pos,x,y) = (abs(1.*x-1.*y)<0.1) ? pos: -1000

set xlabel timeLabel
set ylabel positionLabel
unset key
set pm3d map

set yrange [0:]
set out trafficlightsOutput
splot\
 trafficlightFile using ($1):(deltaTL($2,0,$3)):(1) title "" with points linestyle 16,\
 trafficlightFile using ($1):(deltaTL($2,1,$3)):(1) title "" with points linestyle 17,\
 trafficlightFile using ($1):(deltaTL($2,2,$3)):(1) title "" with points linestyle 18,\
 trafficlightFile using ($1):(deltaTL($2,3,$3)):(1) title "" with points linestyle 19,\
 trafficlightFile using ($1):(deltaTL($4,0,$5)):(1) title "" with points linestyle 16,\
 trafficlightFile using ($1):(deltaTL($4,1,$5)):(1) title "" with points linestyle 17,\
 trafficlightFile using ($1):(deltaTL($4,2,$5)):(1) title "" with points linestyle 18,\
 trafficlightFile using ($1):(deltaTL($4,3,$5)):(1) title "" with points linestyle 19,\
 carFile(1)  using ($1):($4):(1) title car(1) with lines linestyle 1,\
 carFile(4)  using ($1):($4):(1) title car(2) with lines linestyle 1,\
 carFile(8)  using ($1):($4):(1) title car(2) with lines linestyle 1,\
 carFile(12) using ($1):($4):(1) title car(3) with lines linestyle 1,\
 carFile(16) using ($1):($4):(1) title car(4) with lines linestyle 1,\
 carFile(20) using ($1):($4):(1) title car(5) with lines linestyle 1
